apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/../../doc"
}

def props = new Properties()
file("../secret.properties").withInputStream {
    stream -> props.load(stream)
}

def configArtifactId = 'sdk'
def configGroupId = 'com.pushpushgo'

android {
    compileSdkVersion 29
    buildToolsVersion = '29.0.3'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 3
        versionName "0.1.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASE_URL", '"' + props.BASE_URL + '"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
    dexOptions {
        javaMaxHeapSize "12g"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Kodein
    //noinspection GradleDependency
    implementation "org.kodein.di:kodein-di-generic-jvm:6.2.1"

    // Kotlin Android Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.6'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.6'

    // Retrofit
    def retrofit_version = '2.6.4'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.11'
    implementation 'com.google.code.gson:gson:2.8.6'

    // work manager
    def work_version = "2.3.4"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.work:work-gcm:$work_version"

    // firebase
    implementation 'com.google.firebase:firebase-core:17.4.1'
    implementation 'com.google.firebase:firebase-messaging:20.1.7'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar

                groupId = configGroupId
                artifactId = configArtifactId
                version = android.defaultConfig.versionName
            }
            debug(MavenPublication) {
                from components.debug
                artifact androidSourcesJar

                groupId = configGroupId
                artifactId = configArtifactId + '-debug'
                version = android.defaultConfig.versionName
            }
        }
    }
}
