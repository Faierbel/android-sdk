plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
    id 'org.jetbrains.kotlinx.binary-compatibility-validator' version '0.5.0'
}

apply from: 'jacoco.gradle'

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/../../doc"
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 11
        versionName "1.1.0-SNAPSHOT"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            buildConfigField "String", "BASE_URL", '"https://api.pushpushgo.com/"'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "String", "BASE_URL", '"https://api-ddd.qa.qappg.co/"'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Kodein
    //noinspection GradleDependency
    implementation "org.kodein.di:kodein-di-generic-jvm:6.2.1"

    // Kotlin Android Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3'

    // Retrofit
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    def moshi_version = "1.12.0"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    // work manager
    def work_version = "2.5.0"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.work:work-gcm:$work_version"

    // firebase
    compileOnly platform('com.google.firebase:firebase-bom:26.2.0')
    compileOnly 'com.google.firebase:firebase-core'
    compileOnly 'com.google.firebase:firebase-messaging'

    // HMS
    compileOnly 'com.huawei.hms:push:5.1.1.301'

    // testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.11.0"
    testImplementation 'org.json:json:20180813'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

def configArtifactId = 'sdk'
def configGroupId = 'com.pushpushgo'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = configGroupId
                artifactId = configArtifactId
                version = android.defaultConfig.versionName
            }
            debug(MavenPublication) {
                from components.debug
                artifact androidSourcesJar

                groupId = configGroupId
                artifactId = configArtifactId + '-debug'
                version = android.defaultConfig.versionName
            }
        }
        repositories {
            maven {
                name "PPGo"
                url "https://gitlab.goodylabs.com/api/v4/projects/297/packages/maven"
                credentials(HttpHeaderCredentials) {
                    name = "Private-Token"
                    value = System.getenv("GITLAB_PRIVATE_TOKEN")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }
}
