apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply from: 'jacoco.gradle'

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/../../doc"
}

android {
    compileSdkVersion 29
    buildToolsVersion = '29.0.3'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 8
        versionName "0.1.7"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            buildConfigField "String", "BASE_URL", '"https://api.pushpushgo.com/"'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "String", "BASE_URL", '"https://api.stppg.co/"'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
    dexOptions {
        javaMaxHeapSize "12g"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Kodein
    //noinspection GradleDependency
    implementation "org.kodein.di:kodein-di-generic-jvm:6.2.1"

    // Kotlin Android Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'

    // Retrofit
    def retrofit_version = '2.6.4'
    //noinspection GradleDependency
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    //noinspection GradleDependency
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.12'

    implementation "com.squareup.moshi:moshi:1.9.3"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.9.3"
    implementation 'com.squareup.moshi:moshi-adapters:1.9.3'

    // work manager
    def work_version = "2.4.0"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.work:work-gcm:$work_version"

    // firebase
    implementation 'com.google.firebase:firebase-core:17.5.0'
    implementation 'com.google.firebase:firebase-messaging:20.2.4'

    // testing
    testImplementation 'junit:junit:4.13'
    testImplementation "io.mockk:mockk:1.10.0"
    testImplementation 'org.json:json:20180813'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

def configArtifactId = 'sdk'
def configGroupId = 'com.pushpushgo'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = configGroupId
                artifactId = configArtifactId
                version = android.defaultConfig.versionName
            }
            debug(MavenPublication) {
                from components.debug
                artifact androidSourcesJar

                groupId = configGroupId
                artifactId = configArtifactId + '-debug'
                version = android.defaultConfig.versionName
            }
        }
    }
}
